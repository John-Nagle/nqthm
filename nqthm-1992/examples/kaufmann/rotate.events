#|

 Copyright (C) 1994 by Computational Logic, Inc.  All Rights Reserved.

 This script is hereby placed in the public domain, and therefore unlimited
 editing and redistribution is permitted.

 NO WARRANTY

 Computational Logic, Inc. PROVIDES ABSOLUTELY NO WARRANTY.  THE EVENT SCRIPT
 IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESS OR IMPLIED,
 INCLUDING, BUT NOT LIMITED TO, ANY IMPLIED WARRANTIES OF MERCHANTABILITY AND
 FITNESS FOR A PARTICULAR PURPOSE.  THE ENTIRE RISK AS TO THE QUALITY AND
 PERFORMANCE OF THE SCRIPT IS WITH YOU.  SHOULD THE SCRIPT PROVE DEFECTIVE, YOU
 ASSUME THE COST OF ALL NECESSARY SERVICING, REPAIR OR CORRECTION.

 IN NO EVENT WILL Computational Logic, Inc. BE LIABLE TO YOU FOR ANY DAMAGES,
 ANY LOST PROFITS, LOST MONIES, OR OTHER SPECIAL, INCIDENTAL OR CONSEQUENTIAL
 DAMAGES ARISING OUT OF THE USE OR INABILITY TO USE THIS SCRIPT (INCLUDING BUT
 NOT LIMITED TO LOSS OF DATA OR DATA BEING RENDERED INACCURATE OR LOSSES
 SUSTAINED BY THIRD PARTIES), EVEN IF YOU HAVE ADVISED US OF THE POSSIBILITY OF
 SUCH DAMAGES, OR FOR ANY CLAIM BY ANY OTHER PARTY.
|#

;; See CLI Internal Note 185 for a tutorial introduction to the
;; Boyer-Moore prove based on this example.

(BOOT-STRAP NQTHM)

(DEFN ROTATE (N LST)
  (IF (ZEROP N)
      LST
    (ROTATE (SUB1 N)
            (APPEND (CDR LST) (LIST (CAR LST))))))

(DEFN LENGTH (X)
  (IF (LISTP X)
      (ADD1 (LENGTH (CDR X)))
    0))

(DEFN PROPERP (X)
  (IF (LISTP X)
      (PROPERP (CDR X))
    (EQUAL X NIL)))

(DEFN FIRSTN (N LST)
  (IF (ZEROP N)
      NIL
    (CONS (CAR LST)
          (FIRSTN (SUB1 N) (CDR LST)))))

(DEFN NTHCDR (N LST)
  (IF (ZEROP N)
      LST
    (NTHCDR (SUB1 N) (CDR LST))))

(PROVE-LEMMA ASSOCIATIVITY-OF-APPEND (REWRITE)
  (EQUAL (APPEND (APPEND X Y) Z)
         (APPEND X (APPEND Y Z))))

(PROVE-LEMMA APPEND-NIL (REWRITE)
  (IMPLIES (PROPERP X)
           (EQUAL (APPEND X NIL)
                  X)))

(DEFN ROTATE-APPEND-INDUCTION (N LST EXTRA)
  (IF (OR (EQUAL N 0) (NOT (NUMBERP N)))
      T
    (IF (NLISTP LST)
        T
      (ROTATE-APPEND-INDUCTION
       (SUB1 N)
       (CDR LST)
       (APPEND EXTRA (LIST (CAR LST)))))))

(PROVE-LEMMA PROPERP-APPEND (REWRITE)
  (EQUAL (PROPERP (APPEND X Y))
         (PROPERP Y)))

(PROVE-LEMMA ROTATE-APPEND ()
  (IMPLIES (AND (PROPERP EXTRA)
                (NOT (LESSP (LENGTH LST) N)))
           (EQUAL (ROTATE N (APPEND LST EXTRA))
                  (APPEND (NTHCDR N LST)
                          (APPEND EXTRA (FIRSTN N LST)))))
  ((INDUCT (ROTATE-APPEND-INDUCTION N LST EXTRA))))

(PROVE-LEMMA NTHCDR-LENGTH (REWRITE)
  (IMPLIES (PROPERP LST)
           (EQUAL (NTHCDR (LENGTH LST) LST)
                  NIL)))

(PROVE-LEMMA FIRSTN-LENGTH (REWRITE)
  (IMPLIES (PROPERP LST)
           (EQUAL (FIRSTN (LENGTH LST) LST)
                  LST)))

(PROVE-LEMMA ROTATE-LENGTH ()
  (IMPLIES (PROPERP LST)
           (EQUAL (ROTATE (LENGTH LST) LST)
                  LST))
  ((USE (ROTATE-APPEND (EXTRA NIL) (N (LENGTH LST))))))

